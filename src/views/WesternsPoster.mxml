<?xml version="1.0" encoding="utf-8"?>
<s:View xmlns:fx="http://ns.adobe.com/mxml/2009"
		xmlns:s="library://ns.adobe.com/flex/spark"
		xmlns:components="components.*"
		
		overlayControls="true" 
		title="{data.fr}"
		
		viewActivate="viewActivateHandler(event)" 
		creationComplete="creationCompleteHandler(event)"
		gesturePan="panImage(event)"
		dataChange="dataChangeHandler(event)" >
	
	
	<fx:Script>
		<![CDATA[
			import mx.events.FlexEvent;
			
			import spark.events.ViewNavigatorEvent;
			import spark.transitions.ZoomViewTransitionMode;
			
			import utils.Mobile;
			
			import vo.WesternVO;
			
			[Bindable] private var _url:String;
			
			private function viewActivateHandler(event:ViewNavigatorEvent):void
			{
				
				poster.addEventListener(TransformGestureEvent.GESTURE_ZOOM,zoomImage);
				poster.addEventListener(TransformGestureEvent.GESTURE_ROTATE,rotateImage);
			}
			
			private function creationCompleteHandler(event:FlexEvent):void
			{
				Mobile.addIOSbtn(backBtn);
			}
			
			private function panImage(e:TransformGestureEvent):void
			{
				e.stopImmediatePropagation();
				
				poster.x += e.offsetX*3;
				poster.y += e.offsetY*3;
				
			}
			
			private function zoomImage(e:TransformGestureEvent):void
			{
				e.stopImmediatePropagation();
				
				// on vérifie un seuil de 0.5 en min et 3 en maxi
				if (e.scaleX < 1 || e.scaleY < 1){
					if (poster.scaleX > 0.5){
						poster.scaleX*=e.scaleX;
						poster.scaleY*=e.scaleY;
					}
					else{
						poster.scaleX=0.5;
						poster.scaleY=0.5;
					}
				}
				else{
					if (poster.scaleX < 3){
						poster.scaleX*=e.scaleX;
						poster.scaleY*=e.scaleY;
					}
					else{
						poster.scaleX=3;
						poster.scaleY=3;
					}
				}
			}
			
			private function rotateImage (e:TransformGestureEvent):void{
				e.stopImmediatePropagation();
				poster.rotation += e.rotation;
			}
			
			private function dataChangeHandler(event:FlexEvent):void
			{
				var western:WesternVO=data as WesternVO;
				if (western){
					_url=western.posterURL;
				}
			}
			
			/**
			 *centrer l'image
			 *
			 */
			private function centerPoster():void{
				poster.x=Mobile.getWidth()/2-(poster.width/2);
				poster.y=Mobile.getHeight()/2-(poster.height/2);
			}
		]]>
	</fx:Script>
	
	<fx:Declarations>
		<s:ZoomViewTransition id="_zoomTransition" 
							  mode="{ZoomViewTransitionMode.OUT}"/>
		
		<s:Button id="backBtn" label="Détail"
				  height="90%"
				  click="navigator.popView(_zoomTransition)"/>
	</fx:Declarations>
	
	<s:viewMenuItems>
		<components:HomeMenuItem appNavigator="{navigator}"/>
	</s:viewMenuItems>
	
	<s:navigationContent/>
	
	<s:Image id="poster" 
			 source="{_url}" resize="centerPoster();"/>
</s:View>
